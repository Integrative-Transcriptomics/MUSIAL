/*
Set meta-information for the project build.
*/
version 'v2.1'
group 'de.tue.it'

println "Project: $project" 
println "Name: $name" 
println "Project directory: $projectDir" 
println "Build directory: $buildDir" 
println "Version: $version" 
println "Group: $project.group" 
println "AntBuilder: $ant" 

/*
The `buildscript` block defines properties (repositories, plugins, ...)
used within the Gradle build process.
*/
buildscript {
    repositories {
        mavenCentral( )
    }
}

/*
Import plugins used during the build process.
*/
import org.apache.tools.ant.filters.*

/*
To prevent duplicate access to output directories a dependency-hierarchy is
established on different tasks.
*/
tasks.whenTaskAdded { task ->
    if ( task.name == 'jar' || task.name == 'processResources' ) {
        task.dependsOn unpackSnpEff
    }
}

/*
This task unzips SnpEff.
*/
task unpackSnpEff( type: Copy ) {
    from zipTree( 'src/main/resources/SnpEff.zip' )
    into 'src/main/resources/'
}

/*
After building, the Jar is copied into the `releases` directory.
*/
task copyJarToReleases( type: Copy ) {
	mkdir 'releases'
	def jarName = "build/libs/" + rootProject.name + "-" + version + ".jar"
    from jarName
    into "releases"
}

/*
Defines the repositories to look up dependencies.
*/
repositories {
    mavenCentral()
    maven {
        url "https://bio.informatik.uni-jena.de/repository/libs-release-oss/"
    }
}

/*
The following plugins are used during the gradle build process.
*/
apply plugin: 'java'

/*
All project dependencies are defined in the following block.
*/
dependencies {
	implementation 'commons-cli:commons-cli:1.5.0'
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'com.github.samtools:htsjdk:3.0.0'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'args4j:args4j:2.33'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.2'
    implementation 'org.biojava:biojava-core:5+'
    implementation 'org.biojava:biojava-genome:5+'
    implementation 'org.biojava:biojava-structure:5+'
    implementation 'me.tongfei:progressbar:0.9.4'
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'org.javatuples:javatuples:1.2'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'net.sourceforge:javaml:0.1.7'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.aayushatharva.brotli4j:brotli4j:1.8.0'
    implementation 'com.aayushatharva.brotli4j:native-windows-x86_64:1.8.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
}

/*
In order to build a final FatJar, all entries from configurations.implementation (the above defined
dependencies) are copied into configurations.includeJars to prevent an internal Gradle error.
*/
configurations {
    includeJars.extendsFrom implementation
}

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

/*
Defines the source directories for the tasks executed by the `java` plugin.
*/
sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

/*
Copies the title and version of the project into resources files.
*/
processResources {
    duplicatesStrategy 'exclude' // If duplicated files exist, they are excluded.
    exclude('*.zip')
    filesMatching('version.properties'){
    	expand projectVersion: version
    }
    filesMatching('title.properties'){
    	expand projectTitle: rootProject.name
    }
}

jar {
    duplicatesStrategy 'exclude' // If duplicated files exist, they are excluded.
    manifest {
        attributes("Implementation-Title": rootProject.name,
                "Implementation-Version": archiveVersion,
                "main-Class": "main.Musial")
    }
    doFirst {
        from { configurations.includeJars.collect { it.isDirectory() ? it : zipTree(it) } } // Collects all Jars from dependencies and builds a FatJar.
    }
}

/*
Defines steps executed by calling `gradle clean`.
*/
clean.doFirst {
	delete "${rootDir}/releases/"
    delete "${rootDir}/build/"
    delete "${rootDir}/src/main/resources/snpEff"
}

/*
After building the FatJar, it is copied to the releases directory.
*/
build.finalizedBy( copyJarToReleases )